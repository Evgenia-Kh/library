метод ПриИзмененииКниги(Источник: ПолеВвода<Книги.Ссылка?>, Событие: СобытиеПриИзменении<Книги.Ссылка?>)
    ЗаполнитьДанныеКниги(Объект.Книга)
;
 
@НаСервере @НаКлиенте
структура ДанныеКниги
    пер Автор: Авторы.Ссылка?
    пер Тематика: Тематика.Ссылка?
    пер ФорматКниги: Формат
    пер Язык: Язык
;

@НаСервере @ДоступноСКлиента
статический метод ПолучитьДанныеКниги(Книга: Книги.Ссылка): ДанныеКниги

    пер Запрос = Запрос{ВЫБРАТЬ
        Книги.Автор КАК Автор,
        Книги.Тематика КАК Тематика,
        Книги.Формат КАК Формат,
        Книги.Язык КАК Язык
    ИЗ
        Книги КАК Книги
    ГДЕ
        Книги.Ссылка == %Книга}
        
    знч ДанныеБазы = Запрос.Выполнить().Единственный()
    
    пер ДанныеКниги = новый ДанныеКниги()
    ДанныеКниги.Автор = ДанныеБазы.Автор
    ДанныеКниги.Тематика = ДанныеБазы.Тематика
    ДанныеКниги.ФорматКниги = ДанныеБазы.Формат
    ДанныеКниги.Язык = ДанныеБазы.Язык
    возврат ДанныеКниги
;    

@Обработчик
метод ПослеЧтения()
    ЗаполнитьДанныеКниги(Объект.Книга)
;

метод ЗаполнитьДанныеКниги(Книга: Книги.Ссылка?)
    если Книга == Неопределено
        возврат
    ;
    знч ДанныеКниги = ПолучитьДанныеКниги(Книга)
    Компоненты.Автор.Значение = ДанныеКниги.Автор
    Компоненты.Тематика.Значение = ДанныеКниги.Тематика
    Компоненты.ФорматКниги.Значение = ДанныеКниги.ФорматКниги
    Компоненты.Язык.Значение = ДанныеКниги.Язык
;